&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Заголовок") Тогда
		Заголовок  = Параметры.Заголовок;
	КонецЕсли;

	Если Параметры.Свойство("ВозвращатьТолькоВыбранныеЗначения") Тогда
		ВозвращатьТолькоВыбранныеЗначения=Параметры.ВозвращатьТолькоВыбранныеЗначения;
	КонецЕсли;

	СписокЗначений=Параметры.Список;
	Если Параметры.Свойство("ТипЭлементов") Тогда
		Если Параметры.ТипЭлементов <> Неопределено И Параметры.ТипЭлементов <> Новый ОписаниеТипов Тогда
			СписокЗначений.ТипЗначения=Параметры.ТипЭлементов;
		КонецЕсли;
	КонецЕсли;

	Если Параметры.Свойство("ВидимостьПометки") Тогда
		Элементы.СписокЗначенийПометка.Видимость=Параметры.ВидимостьПометки;
	КонецЕсли;
	Если Параметры.Свойство("ВидимостьПредставления") Тогда
		Элементы.СписокЗначенийПредставление.Видимость=Параметры.ВидимостьПредставления;
	КонецЕсли;

	Если Параметры.Свойство("РежимПодбора") Тогда
		РежимПодбора=Параметры.РежимПодбора;
	Иначе
		РежимПодбора=Ложь;
	КонецЕсли;

	Элементы.СписокЗначений.ИзменятьПорядокСтрок=РежимПодбора;
	Элементы.СписокЗначений.ИзменятьСоставСтрок=РежимПодбора;
	Элементы.СписокЗначенийЗначение.ТолькоПросмотр=Не РежимПодбора;
	Если Не РежимПодбора Тогда
		Элементы.СписокЗначений.ПоложениеКоманднойПанели=ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	КонецЕсли;

	Если Параметры.Свойство("ДоступныеЗначения") Тогда
		Элементы.СписокЗначенийЗначение.РежимВыбораИзСписка=Истина;
		Элементы.СписокЗначенийЗначение.СписокВыбора.Очистить();

		Для Каждого ЭлементСписка Из Параметры.ДоступныеЗначения Цикл
			Элементы.СписокЗначенийЗначение.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление,
				ЭлементСписка.Пометка, ЭлементСписка.Картинка);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
&НаКлиенте
Процедура Применить(Команда)
	Если Не ВозвращатьТолькоВыбранныеЗначения Тогда
		СписокВозврата=СписокЗначений;
	Иначе
		СписокВозврата=Новый СписокЗначений;

		Для Каждого Элемент Из СписокЗначений Цикл
			Если Не Элемент.Пометка Тогда
				Продолжить;
			КонецЕсли;
			СписокВозврата.Добавить(Элемент.Значение, Элемент.Представление, Элемент.Пометка, Элемент.Картинка);
		КонецЦикла;
	КонецЕсли;

	Закрыть(СписокВозврата);
КонецПроцедуры